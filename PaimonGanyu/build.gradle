plugins {
    id "jacoco"
    id "org.sonarqube" version "3.3"
}

allprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "org.sonarqube"

    version = "0.5"

    ext {
        bootVersion = '2.4.2'
        awsSdkVersion = '1.11.914'
        jacksonVersion = '2.11.4'
        lombokVersion = '1.18.22'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.3.1'
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.6.28'
        testImplementation 'org.assertj:assertj-core:3.18.1'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    }

    task buildZip(type: Zip) {
        from compileJava
        from processResources
        into('lib') {
            from configurations.runtimeClasspath
        }
    }

    task copyBuiltZip(type: Copy) {
        def pathOut = '../'.repeat(project.depth)
        from buildZip
        into("${pathOut}.aws-sam/build")
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport.dependsOn test
    test.finalizedBy jacocoTestReport

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

project(':application').dependencies {
    implementation project(':hoyoapi')
    implementation project(':awsutils')
    implementation (project(':ikakao')) {
        because 'to render KAKAO chatbot views'
    }
    implementation project(':domain')
}