plugins {
    id "java"
    id "jacoco"
    id "org.sonarqube" version "3.3"
    id "org.springframework.boot" version '2.4.2'
    id "io.spring.dependency-management" version '1.0.11.RELEASE'
}

allprojects {
    apply plugin: "java-library"
    apply plugin: "jacoco"
    apply plugin: "org.sonarqube"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    version = "0.5"

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        //implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        testCompileOnly 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

        testCompileOnly 'org.assertj:assertj-core:3.18.1'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    task buildZip(type: Zip) {
        from compileJava
        from processResources
        into('lib') {
            from configurations.runtimeClasspath
        }
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport.dependsOn test
    test.finalizedBy jacocoTestReport
}

project(':web').dependencies {
    implementation (project(':ikakao')) {
        because 'To render Kakao chatbot views'
    }
    implementation project(':domain')
    implementation project(':infra')
}

project(':lambda').dependencies {
    implementation project(':awsutils')
    implementation project(':domain')
    implementation project(':infra')
}

project('infra').dependencies {
    implementation project(':domain')
}

// TODO: domain must not depends on other sibling modules
project(':domain').dependencies {
    testImplementation project(':hoyoapi')
    testImplementation project(':infra')
}