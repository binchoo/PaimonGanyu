plugins {
    id 'java'
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    /* Spring Framework*/
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Amazon DynamoDB
    implementation ('com.amazonaws:aws-java-sdk-dynamodb:1.11.914') {
        because 'All workflows use the Amazon DynamoDB as their persistence system.'
    }
    implementation ('com.github.derjust:spring-data-dynamodb:5.1.0') {
        because 'To automatically create some DynamoDB repositories.'
    }

    // Amazon SNS
    implementation ('com.amazonaws:aws-java-sdk-sns:1.11.914') {
        because 'Hoyopass fanout leverages an SNS topic.'
    }

    // AWS Systems Manager
    implementation ('com.amazonaws:aws-java-sdk-ssm:1.11.914') {
        because 'Hoyopass could digest secured (RSA-signed) hoyopass strings.'
    }

    // AWS Lambda
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.0'

    runtimeOnly 'com.h2database:h2'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    testCompileOnly 'org.assertj:assertj-core:3.18.1'
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

test {
    useJUnitPlatform()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jacocoTestReport.dependsOn test
test.finalizedBy jacocoTestReport

