plugins {
    id "jacoco"
    id "org.sonarqube" version "3.3"
}

allprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "org.sonarqube"

    ext {
        bootVersion = '2.4.2'
        awsSdkVersion = '1.11.914'
        jacksonVersion = '2.11.4'
        lombokVersion = '1.18.22'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.3.1'
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.6.28'
        testImplementation 'org.assertj:assertj-core:3.18.1'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    }

    task buildZip(type: Zip) {
        from compileJava
        from processResources
        into('lib') {
            from configurations.runtimeClasspath
        }
    }

    task copyBuiltZip(type: Copy) {
        def dest = '../'.repeat(project.depth + 1) + '.aws-sam/build'
        from buildZip
        into(dest)
        doLast {
            println "$project.name:$name has moved artifacts into $dest"
        }
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport.dependsOn test
    test.finalizedBy jacocoTestReport

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

project(':application').dependencies {
    implementation project(':hoyoapi')
    implementation project(':awsutils')
    implementation (project(':ikakao')) {
        because 'to render skill response of kakao chatbot'
    }
    implementation project(':domain')
    testImplementation(testFixtures(project(':domain')))
}